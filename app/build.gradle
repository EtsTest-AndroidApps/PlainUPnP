apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.m3sv.plainupnp"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        versionCode versions.versionCode
        versionName versions.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "store"

    productFlavors {
        playstore {
            dimension "store"
        }

        fdroid {
            dimension "store"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.kotlin
    implementation deps.coroutines

    androidTestImplementation deps.test.core
    androidTestImplementation deps.test.rules
    androidTestImplementation deps.test.espresso.core
    androidTestImplementation deps.test.assertions.junit

    testImplementation deps.test.core
    testImplementation deps.test.rules
    testImplementation deps.test.assertions.junit
    testImplementation deps.test.mockito.core
    testImplementation deps.test.coreTesting

    implementation deps.androidx.leanback

    implementation deps.androidx.navigation.fragment
    implementation deps.androidx.navigation.ui

    implementation deps.support.appCompat
    implementation deps.support.v4
    implementation deps.support.design
    implementation deps.support.recyclerView
    implementation deps.support.cardView
    implementation deps.support.constraintLayout
    implementation deps.support.preferences
    implementation deps.architectureComponents.lifecycle
    implementation deps.architectureComponents.lifecycleReactiveStreams
    implementation deps.timber
    implementation deps.rx.kotlin
    implementation deps.rx.android
    implementation(deps.glide.core) {
        exclude group: "com.android.support"
    }

    kapt deps.glide.compiler

    implementation deps.dagger.core
    compileOnly deps.dagger.annotation
    kapt deps.dagger.compiler
    implementation deps.dagger.android
    implementation deps.dagger.androidSupport
    kapt deps.dagger.androidProcessor

    // Cling
    implementation group: 'org.fourthline.cling', name: 'cling-core', version: versions.cling
    implementation group: 'org.fourthline.cling', name: 'cling-support', version: versions.cling
    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: versions.jetty
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: versions.jetty
    implementation group: 'org.eclipse.jetty', name: 'jetty-client', version: versions.jetty
    implementation 'org.slf4j:slf4j-jdk14:1.7.14'

    implementation project(':common')
    implementation project(':upnp')
}

kapt {
    generateStubs = true
}

detekt {
    toolVersion = "1.0.0"
    input = files("src/main/java")
    filters = ".*/resources/.*,.*/build/.*"
}


apply plugin: 'com.google.gms.google-services'